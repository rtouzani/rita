# rita Script file to solve the 2-D incompressible Navier-Stokes equations
# Time discretization: Second-order projection method
# Space discretization: Stabilized P1/P1 finite element
#
# Generate mesh of a domain with a step
mesh
  point  label=1  coord=3.0,0.0   size=0.1
  point  label=2  coord=10.0,0.0  size=0.1
  point  label=3  coord=10.0,3.0  size=0.1
  point  label=4  coord=0.0,3.0   size=0.1
  point  label=5  coord=0.0,1.0   size=0.1
  point  label=6  coord=3.0,1.0   size=0.05
  curve  label=1  line=1,2
  curve  label=2  line=2,3
  curve  label=3  line=3,4
  curve  label=4  line=4,5
  curve  label=5  line=5,6
  curve  label=6  line=6,1
  contour  label=1  curves=1,2,3,4,5,6
  surface  label=1  contours=1
  code  value=11  points=1,2,3,4,5,6
  code  value=11  curves=1,3,5,6
  code  value=21  curves=4
  code  value=01  curves=2
  code  value=01  surfaces=1
  save  geo=example5.geo
  nbdof 2
  generate 
  save  mesh=example5.m
  end
#
# Define model: PDE and discretization method
transient  final-time=1.0  time-step=0.01  scheme=builtin
pde incompressible-navier-stokes
  field v
  field p
  init  value=0.
  bc  code=1  value=0.
  bc  code=2  value=(y-1)*(3-y)
  space feP1
  end
# Solve problem, save solution in file
solve
  save  field=v  file=v.pos  format=gmsh  frequency=2
  save  field=p  file=p.vtk  format=vtk   frequency=1
  run
exit
